plugins {
    id 'java'
    id 'antlr'
    id 'application'
}

group 'ca.griis'
version '0.0.1'

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: 'org.apache.logging.log4j', module: ''
}

dependencies {
    implementation "org.antlr:antlr4-runtime:4.9.2"
    antlr "org.antlr:antlr4:4.9.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.16, 3[")
                prefer("2.16.0")
            }
            because("CVE-2021-44228: Log4j vulnerable to remote code execution")
        }
    }
}

generateGrammarSource {
    arguments += ["-visitor",
                  "-listener",
                  "-lib", "src/main/antlr/ca/griis/mMec/imports",
                  "-package", "ca.griis.mMec"
    ]
    outputDirectory = file("src/gen/java")
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/gen/java']
        }
    }
}

test {
    useJUnitPlatform()
}
